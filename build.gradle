buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}

apply plugin: 'download-task'
// Project:
version = '0.1'

// Java components
apply plugin: 'java'

// IDE support
apply plugin: 'idea'

// Run support
apply plugin: 'application'

// Language support
sourceCompatibility = 1.8

// Repositories
repositories {
    mavenCentral()
}

Map filesToDownload = [
        "testRight.wav"   : "https://www.dropbox.com/s/cgbe8pfs6j96az6/testRight.wav?dl=1",
        "testLeft.wav"   : "https://www.dropbox.com/s/x3chpui7hzmqayq/testLeft.wav?dl=1",
        "S1070004.mp4": "https://www.dropbox.com/s/ngitialw9omy250/S1070004.MP4?dl=1",
        "output.mov" : "https://www.dropbox.com/s/02u6ncxcl7bvi45/output.mov?dl=1"
]

// Create Downloading task
def resDir = "src/main/resources/";
filesToDownload.each() { fileName, url ->
    def filePath = resDir + fileName
    File file = new File(filePath);
    if (!file.exists()) {
        task "downloading $fileName" << {
            download {
                src url
                dest filePath
            }
        }
    } else {
        println "$filePath already present"
    }
}

processResources.dependsOn {
    tasks.findAll { task -> task.name.startsWith('downloading') }
}

// External
dependencies {
    // OpenCV bindings
    compile fileTree(dir: 'lib/javacv-bin', include: '*.jar')

    // Google collections (Guava)
    compile group: 'com.google.guava', name: 'guava', version: '16.0.1'

    // Logging
    compile([group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'],
            [group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5'])

    // Junit
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

// Location of main (for running)
mainClassName = "fr.xebia.bumblebee.CameraTest"